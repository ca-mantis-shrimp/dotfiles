@document.meta
title: Default Autocommands
description: Neovim autocommands defined at the start
authors: Darrion Burgess
categories: Neovim Config
tangle: ../../../dot_config/remove_nvim/lua/config/autocmds.fnl
created: 2025-08-31T09:08:46-0800
updated: 2026-01-13T23:48:08-0800
version: 1.1.1
@end
`autocmd` is a powerful api that lets us hook into various pieces of Neovim to extend or otherwise change it dynamically at runtime based on certain conditions.

But they are also very brittle and easy to mess up so absolute discipline is necessary and if you can do it without an autocommand then you should try
* Highlight Yanks before yanking them
  just to add a bit of visual feedback we add a quick highlight right before we yank something so we can visually see what we grabbed
  #tangle
  @code fennel
  (_G.vim.api.nvim_create_autocmd :TextYankPost
                                  {:desc "highlight when yanking text"
                                   :group (_G.vim.api.nvim_create_augroup :kickstart-highlight-yank
                                                                          {:clear true})
                                   :callback (fn []
                                               (_G.vim.highlight.on_yank))})
  @end
** Setup LSP-related plugins after initial startup
   since lsp hotkeys can only be setup AFTER the lsp is actually attached, this is best done as an autocommand to get this done for all languages with an LSP while no polluting the keymaps for languages where its unnecesary

*** LSP Callback
    the work itself is a function that is called after the proper trigger event
    here we are actually going to be working with a few hotkey types so to go over them:
    most of them reside behind `<leader>`
    - `rn` [r]e[n]ames the variable
    - `ca` brings up the [c]ode [a]ction options
    - `K` allows for hover documentation
    -- so fundamental we keep it under homerow even if it doesnt make as much phonetic sense and `K` is the traditional documentation hotkey
    - `gD` [g]oes to [D]eclaration
    - `th` [t]oggles inlay [h]ints

    we also create a few autocommands to:
    - highlight all instances of a variable that our cursor is hovering over
    -- and stop when we move cursor 
    #tangle
    @code fennel
    (fn setup_lsp [event]
      (local setup_lsp_map
             (fn [keys func desc]
               (_G.vim.keymap.set :n keys func
                                  {:buffer event.buf :desc (.. "LSP: " desc)})))
      (setup_lsp_map :<leader>rn _G.vim.lsp.buf.rename "[R]e[n]ame")
      (setup_lsp_map :<leader>ca _G.vim.lsp.buf.code_action "[C]ode [A]ction")
      (setup_lsp_map :K _G.vim.lsp.buf.hover "Hover Documentation")
      (setup_lsp_map :gD _G.vim.lsp.buf.declaration "[G]oto [D]eclaration")
      (local client (_G.vim.lsp.get_client_by_id event.data.client_id))
      (when (and client client.server_capabilities.documentHighlightProvider)
        (_G.vim.api.nvim_create_autocmd [:CursorHold :CursorHoldI]
                                        {:buffer event.buf
                                         :callback _G.vim.lsp.buf.document_highlight})
        (_G.vim.api.nvim_create_autocmd [:CursorMoved :CursorMovedI]
                                        {:buffer event.buf
                                         :callback _G.vim.lsp.buf.clear_references}))
      (when (and client client.server_capabilities.inlayHintProvider
                 _G.vim.lsp.inlay_hint)
        (setup_lsp_map :<leader>th
                       (fn []
                         (_G.vim.lsp.inlay_hint.enable 0
                                                       (not (_G.vim.lsp.inlay_hint.is_enabled))))
                       "[T]oggle Inlay [H]ints")))
    @end
*** Creating the autocommand
    all we do now is run the function above from the `LspAttach` event.

    I will also note this is part of the `lsp-attach` autocommand group to denote this is meant to be done after we have attached an lsp server
    #tangle
    @code fennel
    (_G.vim.api.nvim_create_autocmd :LspAttach
                                    {:group (_G.vim.api.nvim_create_augroup :lsp-attach
                                                                            {:clear true})
                                     :callback setup_lsp})
    @end

* Configure Local Treesitter Parser for Development
  For local development of our tree-sitter-actions grammar we will setup an autocmd to update `TSUpdate` to properly use our local repository

  ** Understanding the Configuration
     The new nvim-treesitter (main branch rewrite) requires using a `User TSUpdate` autocommand to register custom parsers.

     Key learnings:
     - The `path` field points to the local grammar directory
     - The `queries` field must point to the subdirectory containing the .scm files
       - WRONG: `queries` (creates double-nesting: queries/actions/actions/)
       - RIGHT: `queries/actions` (creates correct structure: queries/actions/)
     - nvim-treesitter will symlink the queries directory to `~/.local/share/nvim/site/queries/actions`
     - The parser .so file goes to `~/.local/share/nvim/site/parser/actions.so`

  ** Query Syntax Requirements
     For concealment to work properly:
     - Use double parentheses for standalone patterns: `((node) @capture (#set! conceal "icon"))`
     - Empty concealment requires explicit empty string: `((node) @conceal (#set! conceal ""))`
     - Anonymous tokens (like `[`, `]`, `>`) must be named nodes in grammar to be capturable
     - The `concealcursor` setting must exclude modes where you want concealment visible
       - `concealcursor=` (empty) shows concealment in all modes
       - `concealcursor=nc` hides concealment in normal/command mode (bad for editing)

    #tangle
    @code fennel
    (vim.api.nvim_create_autocmd :User
                                 {:pattern :TSUpdate
                                  :callback (fn []
                                              ;; Configure local actions parser for development
                                              (tset (require :nvim-treesitter.parsers)
                                                    :actions
                                                    {:install_info {:path "~/Products/platform/tree-sitter-actions"
                                                                    :queries :queries/actions}
                                                     :filetype :actions}))})
    @end
